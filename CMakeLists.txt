cmake_minimum_required(VERSION 3.31)

project(cuphttp VERSION 1.0.0)

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")
if(CMAKE_PROJECT_NAME STREQUAL PROJECT_NAME)
    include(CTest)
    include(ExportHelper)
endif()

add_subdirectory(src)

include(CMakePackageConfigHelpers)
configure_package_config_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/cmake/Config.cmake.in
  "${CMAKE_CURRENT_BINARY_DIR}/cmake/${CMAKE_PROJECT_NAME}Config.cmake"
  INSTALL_DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${CMAKE_PROJECT_NAME})

write_basic_package_version_file(
  "${CMAKE_CURRENT_BINARY_DIR}/cmake/${CMAKE_PROJECT_NAME}ConfigVersion.cmake"
  VERSION "${CMAKE_PROJECT_VERSION}"
  COMPATIBILITY AnyNewerVersion)

install(
  FILES
    "${CMAKE_CURRENT_BINARY_DIR}/cmake/${CMAKE_PROJECT_NAME}Config.cmake"
    "${CMAKE_CURRENT_BINARY_DIR}/cmake/${CMAKE_PROJECT_NAME}ConfigVersion.cmake"
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${CMAKE_PROJECT_NAME})

option(STANDALONE_PACKAGE "Create standalone package" OFF)
option(${CMAKE_PROJECT_NAME}_BUILD_EXAMPLES "Build example binaries" OFF)
option(${CMAKE_PROJECT_NAME}_BUILD_TESTING "Build unit tests" OFF)
if(STANDALONE_PACKAGE)
  install(
    CODE "if(NOT DEFINED CPACK_TEMPORARY_INSTALL_DIRECTORY)\nexecute_process(COMMAND ${CMAKE_COMMAND} -DROOT_DIR=${CMAKE_INSTALL_PREFIX} -P ${CMAKE_SOURCE_DIR}/cmake/DepCollect.cmake)\nendif()"
  )
endif()
set(PACKAGE_NAME "${CMAKE_PROJECT_NAME}")
set(CPACK_GENERATOR "TGZ;DEB;RPM")
set(PACKAGE_INSTALL_DIRECTORY "/opt/${PACKAGE_NAME}")
set(CPACK_PACKAGE_NAME ${PACKAGE_NAME})
set(CPACK_DEBIAN_PACKAGE_NAME ${PACKAGE_NAME})
set(CPACK_DEBIAN_PACKAGE_SOURCE ${PACKAGE_NAME})
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Depot C++ Package MaNAGER")
set(CPACK_PACKAGE_VENDOR "Vasyl Zaichenko")
set(CPACK_PACKAGE_DIRECTORY "${CMAKE_SOURCE_DIR}/packages")
set(CPACK_PACKAGE_INSTALL_DIRECTORY ${CPACK_PACKAGE_NAME})
set(CPACK_VERBATIM_VARIABLES ON)
set(CPACK_PACKAGING_INSTALL_PREFIX "${PACKAGE_INSTALL_DIRECTORY}")
set(CPACK_DEBIAN_PACKAGE_CONTROL_STRICT_PERMISSION TRUE)
set(CPACK_DEBIAN_PACKAGE_ARCHITECTURE amd64)

set(CPACK_DEBIAN_PACKAGE_MAINTAINER
    "Vasyl Zaichenko <vasyl.v.zaichenko@gmail.com>")
if(STANDALONE_PACKAGE)
  set(CPACK_RPM_PACKAGE_RELOCATABLE ON)
  set(CPACK_PRE_BUILD_SCRIPTS "${CMAKE_SOURCE_DIR}/cmake/DepCollect.cmake")
else()
  set(CPACK_RPM_PACKAGE_AUTOREQ ON)
  set(CPACK_RPM_PACKAGE_REQUIRES "glibc libstdc++")
  set(CPACK_DEBIAN_PACKAGE_DEPENDS libc6 libstdc++6)
  set(CPACK_DEBIAN_PACKAGE_SHLIBDEPS ON)
  set(CPACK_DEBIAN_PACKAGE_GENERATE_SHLIBS ON)
  set(CPACK_PRE_BUILD_SCRIPTS "${CMAKE_SOURCE_DIR}/cmake/FixLoader.cmake")
endif()

include(CPack)
